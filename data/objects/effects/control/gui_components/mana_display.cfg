//The mana bar displays how much energy for abilities we have.
{
	id: "mana_display",
	is_strict: true,
	use_absolute_screen_coordinates: true,
	hidden_in_game: true,
	serializable: false,
	
	zorder: "@include data/zorder.cfg:in_front_of_everything",
	no_compile_image: "gui/progress-bar-center.png",

	properties: {
		target: "obj player_controlled <- lp asserting lp where lp = level.local_player",
		
		mana_bar_max_length: 114,
		mana_bar_cap_width: 5,
		mana_bar_length: "round(((mana_bar_max_length - mana_bar_cap_width) * target.mana) / target.max_mana)",
		
		mana_bar: {
			type: "bar_widget", 
			init: "bar_widget <- widget(me, {
				type: 'bar',
				segment_length: 50,
				left_cap_length: 50,
				x: 4,
				y: 0,
				bar: {
					image: 'gui/progress-bar-center.png',
					area: [0,0,7,7], //xywh
				},
				left_cap: {
					image: 'gui/progress-bar-endcaps.png',
					area: [3,0,5,7],
				},
				right_cap: {
					image: 'gui/progress-bar-endcaps.png',
					area: [0,8,5,7],
				},
			})",
		},
	
		mana_bar_container: {
			type: "widget", 
			init: "widget(me, {
				type: 'image',
				image: 'gui/hud.png',
				area: [2,131,124,139], //xyxy
			})",
		},
	},
	
	on_create: "[
		set(me.widgets.child, mana_bar),
		set(me.widgets.child, mana_bar_container),
		set(mana_bar.segment_length, mana_bar_length),
	]",
	
	on_process: "[
		set(mana_bar.segment_length, mana_bar_length),
		set(mana_bar.visible, mana_bar_length > 0), //The mana bar is invisible if tapped out.
		//map(widgets.children, set(value.scale, 2/level.zoom)), //Attempts to adjust for scale, mostly fails.
	]",
}

/* PREVIOUSLY, IN hud.cfg:
	//// The blue mana-bar.		                 
	draw_animation('manabar_empty', 48, 32),
	if(level.local_player and level.local_player.mana > 0, ([
		draw_animation('charge_bar_left', x_offset, y_offset),
		draw_animation('charge_bar_right', x_offset + barLength*2, y_offset),
		if(barLength > 5, draw_animation_area('charge_bar_center', x_offset+10, y_offset, barLength-5))
	] where x_offset = 52, 
	        y_offset = 32,
	        barLength = int((fullBarLength*level.local_player.mana)/level.local_player.max_mana))
	  where fullBarLength = 114 ),
*/