//The mana bar displays how much energy for abilities we have.
{
	id: "number_display",
	is_strict: false,
	use_absolute_screen_coordinates: true,
	hidden_in_game: true,
	serializable: false,
	
	zorder: "@include data/zorder.cfg:in_front_of_everything",

	properties: {
		target: "obj player_controlled <- lp asserting lp where lp = level.local_player", //Target object, to have it's information displayed.
		prefix: { type: "string" }, //A prefix for the number displayed; eg, '$' for money. Constant.
		field: { type: "string" }, //Target field in object, target[field] must resolve to an integer value. Could be refactored to do an int|string, though, if needed.
		
		number: {
			type: "widget", 
			init: "widget <- widget(me, {
				type: 'graphical_font_label',
				text: 'number display placeholder text',
				font: 'white_outline', //or 'default'
				size: 1,
			})",
		},
		
		last_displayed_value: { type: "int", default: 0 },
	},
	
	on_create: "[
		set(me.widgets.child, number),
		set(number.text, '${prefix}${int<-target[field]}'), //Note: If you came here wondering why Frogatto's 'coins' gui element doesn't display it's $ prefix, it's because it's not a glyph in the current font.
	]",
	
	on_process: "if(last_displayed_value != (int<-target[field]), [
		set(last_displayed_value, int<-target[field]),
		set(number.text, '${prefix}${int<-target[field]}'),
	])",
}

/* PREVIOUSLY, IN hud.cfg:
	//// Calculate the coin display slowly rolling up or down.
	if(object.vars.coins != (level.local_player and level.local_player.coins or 0)*100, 
		set(object.vars.coins, object.vars.coins + diff/20 + sign(diff))
			where diff = (level.local_player and level.local_player.coins or 0)*100 - object.vars.coins
	),

	
	...


	//// Draw the gold display. The score display is drawn in score.cfg.
	draw_animation('gold_background', 178, 10),
	draw_number(object.vars.coins, 5, 202, 14),
*/



/* PREVIOUSLY, IN score.cfg:
on_process: "[ //Calculate, but do not draw anything yet.
	//// Calculate the score display slowly rolling up or down.
	if(object.vars.score < (level.local_player and level.local_player.score*100 or 0), 
		set(object.vars, 'score', object.vars.score + diff/20 + 1) 
			where diff = (level.local_player and level.local_player.score*100 or 0) - object.vars.score ),
	]",


on_draw: "[
	draw_animation('score_background', 48, 10),
	draw_number(object.vars.score, 7, 52, 14),
]",
*/