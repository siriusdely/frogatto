{
id: "fungus_bracket_springy",
prototype: ["standard_values"],
platform_area: [0,16,79,17],
is_strict: false,
has_feet: false,
zorder: "@include data/zorder.cfg:exterior_decor",
properties: {
	springiness: 1,
	spring_amount: 3500,
	bounce_roundness: 120,
	recombine_pairwise: "def([decimal|[decimal,decimal]] list) -> [[decimal, [decimal,decimal]]]
										 zip(list[0 : list.size/2], list[list.size/2: list.size], [a,b])",
	recombine_serially: "def([[decimal, [decimal,decimal]]] list) -> [decimal|[decimal,decimal]]
										 map(list,value[0]) + map(list,value[1])",
	convert_to_unit_square_coords: "def([[decimal, [decimal,decimal]]] list) -> [[[decimal, decimal], [decimal,decimal]]]
														map(filter(list, value[0] >= 0.0 and value[0] < 1.0), [[(value[0] - 0.5)*2, -1.0] , value[1]]) +
														map(filter(list, value[0] >= 1.0 and value[0] < 2.0), [[1.0,  (value[0] - 1.5)*2] , value[1]]) +
														map(filter(list, value[0] >= 2.0 and value[0] < 3.0), [[-(value[0] - 2.5)*2, 1.0 ] , value[1]]) +
														map(filter(list, value[0] >= 3.0 and value[0]<= 4.0), [[-1.0, -(value[0] - 3.5)*2] , value[1]])",
	flip_if_facing_left_or_upside_down: "def([[[decimal, decimal], [decimal,decimal]]] list) -> [[[decimal, decimal], [decimal,decimal]]]
												map(list, [[value[0][0] * facing,value[0][1] * v_flip] ,value[1]]) where v_flip = -upside_down",
	convert_back_to_serial_coords: "def([[[decimal,decimal], [decimal,decimal]]] list) -> [[decimal, [decimal,decimal]]]
												map(filter(list, value[0][1] = -1.0 and value [0][0] <  1.0), [( value[0][0]/2 + 0.5),value[1]]) + 
												map(filter(list, value[0][0] =  1.0 and value [0][1] <  1.0), [( value[0][1]/2 + 1.5),value[1]]) +
												map(filter(list, value[0][1] =  1.0 and value [0][0] > -1.0), [(-value[0][0]/2 + 2.5),value[1]]) +
												map(filter(list, value[0][0] = -1.0 and value [0][1] > -1.0), [(-value[0][1]/2 + 3.5),value[1]])",
	sort_back_to_serial_order: "def([[decimal, [decimal,decimal]]] list) -> [[decimal, [decimal,decimal]]]
										 sort(list, a[0] < b[0])",
	
	converted_custom_draw_params: "[decimal|[decimal,decimal]] ::
						recombine_serially( sort_back_to_serial_order( convert_back_to_serial_coords( flip_if_facing_left_or_upside_down( convert_to_unit_square_coords( recombine_pairwise( custom_draw_params))))))",

	custom_draw_params: "[decimal|[decimal,decimal]] ::
		(([0.0, 0.25, 0.5, 0.75, 1.0,
		 2.0, 2.25, 2.5, 2.75, 3.0,
		c_upper_edge, map(c_upper_edge, value * 0.60), map(c_upper_edge, value * 0.3), map(c_upper_edge, value * 0.05),still, 
		still, map(c_lower_edge, value * 0.05), map(c_lower_edge, value * 0.3), map(c_lower_edge, value * 0.60), c_lower_edge
		]) where still = [0,0]
			where c_upper_edge = [upper_edge[0]*facing, upper_edge[1]]
			where c_lower_edge = [lower_edge[0]*facing, lower_edge[1]]
			where upper_edge = zip( [x2,mid_y],newPos, a-b)
			where lower_edge = zip( [x2,mid_y],newPos, a-b))
			where newPos = (orbit(x,mid_y, sin(15*cycle)*_magnitude, img_w))",
			
#-------------------------- vars --------------------------#
	_magnitude: { type: "decimal", default: 0, persistent: false },
},


on_process: "[ if(_magnitude > 0.5, set(_magnitude, _magnitude*0.98), set(_magnitude,0)),

if(_magnitude > 0,set(custom_draw, converted_custom_draw_params) )]",


on_jumped_on: "[sound('mushroom-bounce'+1d3+'.wav')]",
on_bounced_on: "[sound('mushroom-bounce'+1d3+'.wav'),set(_magnitude,25)]",
editor_info: {
	category: "props",
},
animation: [
	{
		id: "normal",
		image: "props/fungus-bracket-springy.png",
		rect: [0,0,79,31],
		duration: 200,
		palettes: "@include data/palettes.cfg:forest_palettes",
	},
],
}