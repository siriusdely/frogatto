{
id: "breakable_block",
has_feet: false,
properties: {
	team: "'neutral'",
},
on_die: "[spawn('effect_initiator',mid_x,mid_y,facing,child.death_effects('medium')),
			sound('BlockBreak.ogg'),
			map([0]*3d2, spawn('bouncing_debris_chunk',mid_x, mid_y+1d10, if(1d2=2,1,-1), [add(child.variations, ['rock_small_cement']),child.init_vel('burst')])),
			map([0]*1d2, spawn('bouncing_debris_chunk',mid_x, mid_y+1d10, if(1d2=2,1,-1), [add(child.variations, ['rock_big_cement']),child.init_vel('burst')]))
			]",
on_collide_object_body: "if(collide_with_area = 'attack',
	[if(animation != 'cracked', animation('cracked'), die())  ])",
on_collide_side: "set(velocity_x, -(velocity_x*70)/100)",
on_collide_head: "set(velocity_y, -velocity_y)",
on_collide_feet: "[set(velocity_x, (velocity_x*70)/100), set(velocity_y, if(abs(velocity_y) > 50, -velocity_y/2, 0))]",
zorder: "@include data/zorder.cfg:exterior_decor",
solid_area: [0,0,15,15],
editor_info: {
	category: "props",
},
animation: [
	{
		image: "props/blocks.png",
		body_area: "all",
		id: "normal",
		rect: [0,0,15,15],
		frames: 1,
		duration: 100,
	},
	{
		image: "props/blocks.png",
		body_area: "all",
		id: "cracked",
		rect: [16,0,31,15],
		frames: 1,
		duration: 100,
	},
],
}