{
id: "hypodermic_acorn",
prototype: ["falling_harmful"], #Currently not throwable or hittable. Perhaps should be when in not in 'falling' animaiton?

traction_in_air: 100,
friction: 2000,

properties: {
	acceleration: "100",
	old_mid_xy: "[decimal] :: [_original_mid_x,_original_mid_y]",
},


on_create: "[
	proto_event('falling_harmful','create'),
]",

on_move_towards_player: "[
	set(accel_x, 
		if(level.player.mid_y > mid_y and (
				abs(level.player.mid_x-mid_x) > 25 
				or if(level.player.mid_x < mid_x, velocity_x > 0, velocity_x < 0)),
			if(level.player.mid_x > mid_x, 100, -100)*max(1,velocity_y/100), 
			0
		)
	),
]",

on_process_normal: "[
	set(mid_y, _original_mid_y + sin(cycle*2.5)*10 + sin(cycle*3.1)*5 + sin(cycle*5)*4),
	fire_event('move_towards_player'),
]",

on_detach: "[
	proto_event('falling_harmful','detach'), 
	animation('falling'),
]",

on_process_falling: "[
	fire_event('move_towards_player'),
	set(old_mid_xy, mid_xy),
	set(rotate, 
		lib.math.constrain(
			loop(rotate-10+180), 
			loop(lib.math.angle(old_mid_xy, mid_xy)-90+180), 
			loop(rotate+10+180))-180),
] where loop = lib.math.loop",

on_collide_level: "schedule(1, [
	set(accel_x, 0),
	set(accel_y, 0),
	set(velocity_x, 0),
	set(velocity_y, 0),
	animation('stuck'),
	schedule(50*5, die()),
])",
		
animation: [
	{
		id: "normal",
		image: "experimental/prototypes.png",
		duration: 1,
		frames: 1,
		x:188,y:77,w:21,h:33,
	},{
		id: "falling",
		attack_area: "all",
		image: "experimental/prototypes.png",
		duration: 1,
		frames: 1,
		x:212,y:77,w:21,h:33,
	},{
		id: "stuck",
		image: "experimental/prototypes.png",
		duration: 1,
		frames: 1,
		x:212,y:77,w:21,h:33,
	},
],

}