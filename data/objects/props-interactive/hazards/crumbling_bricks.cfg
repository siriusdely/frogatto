{
id: "crumbling_bricks",
activation_area: 100,
is_strict: true,
prototype: ["standard_values"],
zorder: "@include data/zorder.cfg:interior_decor",
platform_area: [16,16,31,17],
hitpoints: 300,
properties: {
	crumble_per_cycle: "if(higher_difficulty, 10, 2)",
	pick_animation: "animation('health'+((hitpoints/100)+1))",
	material_sound: "'stone'",
},
on_create: "[animation('health4'),set(facing,if(1d2=2,1,-1))]",
on_process: "if(level.player.standing_on = me, [add(hitpoints,-crumble_per_cycle),pick_animation, if(cycle%5=0 and 1d2=2,[spawn('bouncing_debris_chunk',mid_x, mid_y+1d10, if(1d2=2,1,-1), [add(child.variations, ['rock_small_dungeon']),child.init_vel('fall_down')]  ), sound('Rock-Crack'+1d6+'.wav')])])",

on_die: "[
			sound('BlockBreak.ogg'),
			map([0]*3d2, spawn('bouncing_debris_chunk',mid_x, mid_y+1d10, if(1d2=2,1,-1), [add(child.variations, ['rock_small_dungeon']),child.init_vel('fall_down')])),
			map([0]*1d2, spawn('bouncing_debris_chunk',mid_x, mid_y+1d10, if(1d2=2,1,-1), [add(child.variations, ['rock_big_dungeon']),child.init_vel('fall_down')]))
			]",


editor_info: {
	category: "props",
},
animation: [
	{
		"@base": true,
		image: "props/crumbling-bricks.png",
		duration: 10000000,
		feet_x: 24,
		feet_y: 32,
		palettes: "@include data/palettes.cfg:dungeon_palettes",
	},		
	{
		id: "health4",
		rect: [0,0,47,47],
	},
	{
		id: "health3",
		rect: [48,0,95,47],
	},
	{
		id: "health2",
		rect: [96,0,143,47],
	},
	{
		id: "health1",
		rect: [144,0,191,47],
	},
	{
		id: "health0",
		rect: [144,0,191,47],
	},
],
}