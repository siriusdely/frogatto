{
id: "fumper",
properties: {
	physical_size: "int :: 32",
	flinch_threshold: "int :: 40",
	team: "'evil'",
	attack_damage: "int :: if(higher_difficulty, 2, 1)",
	taxonomy: "string :: 'bug'",
	acquirable_item_drop_value: "int :: 20",					
	points_value: "int :: 25",
	
	jump_power_y: "int :: 1100+1d300",
	jump_power_x: "int :: 350+1d100",

	turns_towards_player: "bool :: true",
	should_turn_towards_player: "bool :: ((not facing_towards_player) and turns_towards_player)",

	do_jump: "commands :: if(not is_standing, animation('stand'), [animation('jump'),
				if(facing_towards_player and higher_difficulty,
					[set(velocity_x,1.75*(jump_power_x*facing)),set(velocity_y,-jump_power_y*0.75)],
					[set(velocity_x,jump_power_x*facing),set(velocity_y,-jump_power_y)])])",
					
	set_tilt:	"commands :: if(is_standing, set(rotate, slope_standing_on/3), set(rotate,0))",
},


prototype: ["movable_enemy"],
goes_inactive_only_when_standing: true,

hitpoints: 200,
solid_area: [6,12,22,28],
feet_width: 4,

editor_info: {
	category: "enemies, general",
},

on_create: "spawn('shadow_medium',mid_x,mid_y,1,set(child.parent, me))",

#-------------------------- collision handling --------------------------#
on_collide_feet: "if(animation in ['jump', 'fall'], [animation('spring'), set(velocity_x, velocity_x*0.8)])",
on_process_stand: "if(abs(velocity_x) > 40, set(velocity_x, velocity_x*0.98), set(velocity_x, 0))",
on_process_jump: "[if(velocity_y > 0, animation('fall')),if(velocity_x*facing < jump_power_x, add(velocity_x, jump_power_x*0.05*facing))]",

#-------------------------- animation switching --------------------------#
on_end_anim: "[set_tilt,
				switch(animation,
						'stand', if(should_turn_towards_player and is_standing, turn_around, animation('start_jump')),
						'start_jump', do_jump,
						'jump', if(is_standing, animation('stand'), animation('jump')),
						'fall',	if(is_standing, animation('stand'), animation('fall')),
						animation('stand'))]",


}
