{
id: "kitty",
is_strict: true,
prototype: ["movable_enemy"],
hitpoints: 200,
timer_frequency: 5,
properties: {
	physical_size: "int :: 32",
	flinch_threshold: "int :: 40",
	attack_damage: "int :: 2",
	taxonomy: "string :: 'milgramen'",
	acquirable_item_drop_value: "int :: 16",					
	points_value: "int :: 10",

	choose_action: "commands :: animation('walk')",
	desired_facing: "int :: if(level.player.midpoint_x < self.midpoint_x, -1, 1)",
	face_player: "commands :: if(desired_facing != facing, [animation('turn'),set(facing, desired_facing)], choose_action)",

	springiness: "int :: 200",
	spring_amount: "int :: 1400",
	bounce_roundness: "int :: 80",
	
	turns_towards_player: "bool :: false",
	turns_at_cliffs: "bool :: true",
},
feet_width: 4,
editor_info: {
	category: "enemies, milgramen",
},

#-------------------------- setup --------------------------#
on_create: "[animation('stand'),spawn('shadow_medium_small',mid_x,mid_y,1,set(child.parent, me))]",


#-------------------------- collision handling --------------------------#
on_bounced_on: "[get_hit_sourceless('impact',100), animation('spring'),sound_falloff('kitty-squish.wav')]",

on_collide_side: "if(arg.collide_with != level.player, [set(velocity_x,-velocity_x/2),set(facing, -facing),animation('turn')])",

#-------------------------- animation switching --------------------------#
on_process: "set(rotate, slope_standing_on)",
on_timer: "if(turns_at_cliffs and is_standing and animation = 'walk' and (not is_there_flat_ground_ahead_of_us(30)),[set(facing, -facing),animation('turn')],null)",

on_end_turn_anim: "animation('walk')",
on_end_walk_anim: "choose_action",
on_end_stand_anim: "if(is_standing and not_daydreaming, if(turns_towards_player,face_player, choose_action), animation('stand'))
						where not_daydreaming = if(higher_difficulty, 1d10 > 1, 1d2 = 2)",
on_end_spring_anim: "animation('walk')",

#-------------------------- effects --------------------------#
on_footfall: "sound_falloff('kitty-footstep'+1d10+'.wav')",


}
